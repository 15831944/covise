# - Try to find jpeg-library
# Once done this will define
#
#  JPEG_INCLUDE_DIR    - where to find jpeglib.h, etc.
#  JPEG_INCLUDE_DIRS   - same as above (uncached version)
#  JPEG_LIBRARIES      - list of libraries when using jpeg.
#  JPEG_FOUND          - True if jpeg was found.

IF(JPEG_INCLUDE_DIR)
  SET(JPEG_FIND_QUIETLY TRUE)
ENDIF(JPEG_INCLUDE_DIR)

FIND_PATH(JPEG_INCLUDE_DIR jpeglib.h
   PATHS
   $ENV{JPEG_HOME}/include
   $ENV{EXTERNLIBS}/jpeg/include
   DOC "jpeg - Headers"
   NO_DEFAULT_PATH
)
FIND_PATH(JPEG_INCLUDE_DIR jpeglib.h DOC "jpeg - Headers")
MARK_AS_ADVANCED(JPEG_INCLUDE_DIR)

IF (MSVC)
    # check whether this is a /MT(d) build
    STRING(REGEX MATCH "[mM][tT][dD]" MTD_COMPILE_OPTION ${CMAKE_C_FLAGS_DEBUG})
    IF (MTD_COMPILE_OPTION)
      # MESSAGE("Using static MS-Runtime !!!")
      FIND_LIBRARY(JPEG_LIBRARY_DEBUG NAMES jpegd_mt
        PATHS
        $ENV{JPEG_HOME}/lib
        $ENV{EXTERNLIBS}/jpeg/lib
      )
      FIND_LIBRARY(JPEG_LIBRARY_RELEASE NAMES jpeg_mt
        PATHS
        $ENV{JPEG_HOME}/lib
        $ENV{EXTERNLIBS}/jpeg/lib
      )
    ELSE (MTD_COMPILE_OPTION)
      FIND_LIBRARY(JPEG_LIBRARY_DEBUG NAMES jpegd jpeg-staticd jpegd_i
        PATHS
        $ENV{JPEG_HOME}/lib
        $ENV{EXTERNLIBS}/jpeg/lib
      )
      FIND_LIBRARY(JPEG_LIBRARY_RELEASE NAMES jpeg jpeg-static jpeg_i
        PATHS
        $ENV{JPEG_HOME}/lib
        $ENV{EXTERNLIBS}/jpeg/lib
      )
    ENDIF (MTD_COMPILE_OPTION)

    IF(MSVC_IDE)
      IF (JPEG_LIBRARY_DEBUG AND JPEG_LIBRARY_RELEASE)
         SET(JPEG_LIBRARIES optimized ${JPEG_LIBRARY_RELEASE} debug ${JPEG_LIBRARY_DEBUG})
      ELSE (JPEG_LIBRARY_DEBUG AND JPEG_LIBRARY_RELEASE)
         SET(JPEG_LIBRARIES NOTFOUND)
         MESSAGE(STATUS "Could not find the debug AND release version of jpeg")
      ENDIF (JPEG_LIBRARY_DEBUG AND JPEG_LIBRARY_RELEASE)
    ELSE(MSVC_IDE)
      STRING(TOLOWER ${CMAKE_BUILD_TYPE} CMAKE_BUILD_TYPE_TOLOWER)
      IF(CMAKE_BUILD_TYPE_TOLOWER MATCHES debug)
         SET(JPEG_LIBRARIES ${JPEG_LIBRARY_DEBUG})
      ELSE(CMAKE_BUILD_TYPE_TOLOWER MATCHES debug)
         SET(JPEG_LIBRARIES ${JPEG_LIBRARY_RELEASE})
      ENDIF(CMAKE_BUILD_TYPE_TOLOWER MATCHES debug)
    ENDIF(MSVC_IDE)
    MARK_AS_ADVANCED(JPEG_LIBRARY_DEBUG JPEG_LIBRARY_RELEASE)

ELSE (MSVC)
  
  FIND_LIBRARY(JPEG_LIBRARY NAMES jpeg jpeg-static
    PATHS
    $ENV{JPEG_HOME}/lib
    $ENV{EXTERNLIBS}/jpeg/lib
    NO_DEFAULT_PATH
  )
  FIND_LIBRARY(JPEG_LIBRARY NAMES jpeg jpeg-static)

  IF (JPEG_LIBRARY)
    SET(JPEG_LIBRARIES ${JPEG_LIBRARY})
  ELSE (JPEG_LIBRARY)
    SET(JPEG_LIBRARIES NOTFOUND)
    MESSAGE(STATUS "Could not find jpeg-library")    
  ENDIF (JPEG_LIBRARY)
  MARK_AS_ADVANCED(JPEG_LIBRARY)
  
ENDIF (MSVC)

INCLUDE(FindPackageHandleStandardArgs)

IF(MSVC)
  FIND_PACKAGE_HANDLE_STANDARD_ARGS(JPEG DEFAULT_MSG JPEG_LIBRARY_RELEASE JPEG_LIBRARY_DEBUG JPEG_INCLUDE_DIR)
  MARK_AS_ADVANCED(JPEG_LIBRARY_RELEASE JPEG_LIBRARY_DEBUG)
ELSE(MSVC)
  FIND_PACKAGE_HANDLE_STANDARD_ARGS(JPEG DEFAULT_MSG JPEG_LIBRARY JPEG_INCLUDE_DIR)
  MARK_AS_ADVANCED(JPEG_LIBRARY)
ENDIF(MSVC)
