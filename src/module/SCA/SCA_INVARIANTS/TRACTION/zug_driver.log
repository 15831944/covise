
! first read number of elements and nodes from
! ../zug_conn.dat ../zug_mesh.dat

! get database from embossing
resu,zugmesh,db,../VISUALISATION
/fil,zugmesh

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!! adjustable params
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
FastCase = 1

/prep7

! define traction force
/inp,zug_kraft.log

MyDispPar = MyDispPar * height/100;

!parres,new,info,parm,..
alls
csys,0

*get,nodemax,node,,num,max
*get,elemmax,elem,,num,max

n,nodemax+1,0,0,height
n,nodemax+2,width,0,height
n,nodemax+3,width,height,height
n,nodemax+4,0,height,height
type,1
real,realnorm
e,nodemax+1,nodemax+2,nodemax+3,nodemax+4

! boundary conditions
nsel,s,loc,y,0
dsym,symm,y,0
nsel,s,loc,x,0
dsym,symm,x,0

! choose lower, left corner and clamp it
nsel,r,loc,y,0
d,all,all

! set ROTZ and ROTY on the rightmost line
nsel,s,loc,x,width,2*width
d,all,rotz
d,all,roty

! set ROTZ and ROTX on the upper line
nsel,s,loc,y,height,2*height
d,all,rotz
d,all,rotx

! couple X displacements on the rightmost line
nsel,s,loc,x,width,2*width
cp,next,ux,all

! couple Y displacements on the upper line
nsel,s,loc,y,height,2*height
cp,next,uy,all

alls
save
fini

/solu
*if,FastCase,eq,0,then
   nlgeom,on
   nropt,full

   !define the array of loading (displacement)
   ! MyDispStep = 0.02
   ! MyDispPar=0.2

   MyDispStep = MyDispPar/NumPoints

   *Dim,MyLoad,Table,2,1,,Time
   MyLoad(0,1,1)=0
   MyLoad(1,0,1)=0
   MyLoad(2,0,1)=NumPoints
   MyLoad(2,1,1)=MyDispPar

   !apply loading (displacement)
   nsel,s,loc,y,height-0.0001,height+0.0001
   d,all,,%MyLoad%,,,,uy
   alls

   autots,on

   *do,MyIter,1,NumPoints,1
      time,MyIter
      kbc,0
      ! tsres,erase
      neqit,200
      save
      solve
      lswrite,MyIter
      antype,,rest,last,last,0
   *enddo
*else
   antype,static
   solc,on
   nlgeom,off
   sstiff,off
   !cnvtol,f,1e-5
   !cnvtol,m,1e-4
   MyDispStep = MyDispPar/NumPoints
   autots,on
   *do,MyIter,1,NumPoints,1
      time,MyIter
      !apply loading (displacement)
      nsel,s,loc,y,height-0.0001,height+0.0001
      d,all,uy,MyDispStep*MyIter
      alls
      kbc,0
      solve
      save
   *enddo
*endif


fini

/post1
set,last
*Get,MySetsCount,active,0,Set,LstP ! current load step number

!set the first step for postprocessing
Set,First

!select the nodes for reaction forces definition
*Get,MyNumNodes,Node,,Count
NSel,S,Loc,y,0
NSel,u,node,,nodemax+1
NSel,u,node,,nodemax+2
*Get,MyNumSelNod,Node,,Count

!declare the arrays of node numbers and selected nodes flags
*Dim,MyArNodNum,Array,MyNumSelNod
*Dim,MySelNod,Array,MyNumNodes
!define the selected nodes
*VGet,MySelNod(1),Node,1,NSel

!fill the MyArNodNum array
MyIter=0

*Do,MyCurNode,1,MyNumNodes,1
   *If,MySelNod(MyCurNode),Eq,1,Then
      MyIter=MyIter+1
      MyArNodNum(MyIter)=MyCurNode
   *EndIf
*EndDo

alls

!declare the arrays for the reaction forces and maximum von Mises stresses
*Dim,MySumNodForce,Array,MySetsCount
*Dim,MySumForce,Array,MySetsCount
*Dim,MyMaxEqStr,Array,MySetsCount

!find the sum of reactions at nodes and maximum von Mises stresses
*Do,MyCurStep,1,MySetsCount,1

   MySumNodForce(MyCurStep)=0
   MySumForce(MyCurStep)=0
   *Get,MyNodForce,Node,nodemax+1,Rf,FY
   MySumForce(MyCurStep)=MySumForce(MyCurStep)+MyNodForce
   *Get,MyNodForce,Node,nodemax+2,Rf,FY
   MySumForce(MyCurStep)=MySumForce(MyCurStep)+MyNodForce

   *Do,MyCurNode,1,MyNumSelNod,1
      *Get,MyNodForce,Node,MyArNodNum(MyCurNode),Rf,FY
      MySumNodForce(MyCurStep)=MySumNodForce(MyCurStep)+MyNodForce
   *EndDo

   !reverse the sign and transform to the standard seciment Width = 50 mm
   MySumNodForce(MyCurStep)=-MySumNodForce(MyCurStep)/width*50
   MySumForce(MyCurStep)=-MySumForce(MyCurStep)/width*50

   *Get,MyEqvStr,Node,1,S,Eqv
   MyMaxStrL=MyEqvStr

   *Do,MyElNum,1,MyNumNodes,1
      *Get,MyEqvStr,Node,MyElNum,S,Eqv
      *If,MyEqvStr,GE,MyMaxStrL,Then
         MyMaxStrL=MyEqvStr
      *EndIf
   *EndDo

   MyMaxEqStr(MyCurStep)=MyMaxStrL

   Set,Next

*EndDo

!fill the array of displacements
*Dim,MyDisplData,Array,MySetsCount

*Do,MyIter,1,MySetsCount,1
   !fill and transform to the standard seciment Height = 100 mm
   MyDisplData(MyIter)=MyDispStep*MyIter/height*100
*EndDo

!print values in file
/Output,MyOutput,dat
!remarked the output with the stresses
!*VWrite,MyMaxEqStr(1),MyDisplData(1),MySumNodForce(1)
!(f12.6,'       ',f12.6,'       ',f12.6)
*VWrite,MyDisplData(1),MySumNodForce(1),MySumForce(1)
(f12.6,'        ',f12.6,'        ',f12.6)
/Output

Save

fini

/eof



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! *****************************
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
time,1
! set force...
force = 0.00000001
nsel,s,loc,x,width  !,2*width
nsel,r,loc,y,height !,2*height
f,all,fx,force !!!!!!!!!the force per unit length is 1
alls
solve
!y
fini


/post1
nsort,s,eqv,0,0 ! only relevant in the case of isotrop material
*get,seqvmax,sort,0,max
! loss of mechanical resistance
verlust = 100*(1-force/(height*thickn*seqvmax))
/output,zug_verlust.dat
*vwrite,verlust
(f7.0)
/output

fini
